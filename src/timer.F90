MODULE TIMER
!CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCc
!C     MODULE TIMER
!C     
!C     Module records and reports basic timings of computation,
!C     communication, and idiling of individual processes
!C
!CCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCCc

  USE IFPORT
  USE GLOBAL, ONLY : DTDP, RNDAY
  USE SIZES, ONLY : MNPROC
  IMPLICIT NONE
  
  REAL(8) :: START_TIME, STOP_TIME, CURR_TIME
  REAL(8) :: COMM_TIME, COMPUT_TIME,IDLE_TIME
  REAL(8) :: SL_TIME
  REAL(8) :: MIN_COMM, MIN_COMPUT
  REAL(8) :: TIME_START

  REAL(8), ALLOCATABLE, DIMENSION(:) :: COMM_STORE
  REAL(8), ALLOCATABLE, DIMENSION(:) :: COMPUT_STORE
  REAL(8), ALLOCATABLE, DIMENSION(:) :: SL_STORE
  REAL(8), ALLOCATABLE, DIMENSION(:) :: IDLE_STORE
  INTEGER :: ERR_MSG

  INTEGER :: NSTORES
  INTEGER :: IT_PER_STORE = 1200  
  INTEGER :: CURR_STORE

CONTAINS
  
  SUBROUTINE BEGIN_TIME() !initialize timing
    IMPLICIT NONE
    
    NSTORES = INT(RNDAY*86400/DTDP)/IT_PER_STORE + 1
    ALLOCATE( COMM_STORE(NSTORES) )
    ALLOCATE( COMPUT_STORE(NSTORES) )
    ALLOCATE( IDLE_STORE(NSTORES) )
    ALLOCATE( SL_STORE(NSTORES) )
    CURR_STORE = 1

    MIN_COMM = HUGE(0.D0)
    MIN_COMPUT = HUGE(0.D0)

    SL_TIME = 0.D0
    COMM_TIME = 0.D0
    COMPUT_TIME = 0.D0

      
    START_TIME = DCLOCK()
    CURR_TIME = START_TIME
    TIME_START = CURR_TIME
      
    RETURN
  END SUBROUTINE BEGIN_TIME
    
  SUBROUTINE TIC()
    IMPLICIT NONE
    CURR_TIME = DCLOCK()
    RETURN
  END SUBROUTINE TIC
      
  SUBROUTINE TOC_COMPUT()
    IMPLICIT NONE
    REAL(8) :: TEMP
    TEMP = CURR_TIME
    COMPUT_TIME = COMPUT_TIME - CURR_TIME
    CURR_TIME = DCLOCK()
    COMPUT_TIME = COMPUT_TIME + CURR_TIME
    !compute min time
    TEMP = CURR_TIME - TEMP
    MIN_COMPUT = MIN(TEMP,MIN_COMPUT)
    RETURN
  END SUBROUTINE TOC_COMPUT
    
  SUBROUTINE TOC_COMM()
    IMPLICIT NONE
    REAL(8) :: TEMP
    TEMP = CURR_TIME
    COMM_TIME = COMM_TIME - CURR_TIME
    CURR_TIME = DCLOCK()
    COMM_TIME = COMM_TIME + CURR_TIME

    !comm min time
    TEMP = CURR_TIME - TEMP
    MIN_COMM = MIN(TEMP,MIN_COMM)

    RETURN
  END SUBROUTINE TOC_COMM

  SUBROUTINE TOC_SLOPE_LIM()
    IMPLICIT NONE
    !Here DT = T2 - T1
    SL_TIME = SL_TIME - CURR_TIME
    CURR_TIME = DCLOCK()
    SL_TIME = SL_TIME + CURR_TIME

    RETURN
  END SUBROUTINE TOC_SLOPE_LIM
    

  SUBROUTINE CLOCK_RESET() ! note this ad programmed can only be called during a compute
    IMPLICIT NONE
    REAL(8) :: TEMP
    TEMP = CURR_TIME
    COMPUT_TIME = COMPUT_TIME - CURR_TIME
    CURR_TIME = DCLOCK()
    COMPUT_TIME = COMPUT_TIME + CURR_TIME    
    !compute min time
    TEMP = CURR_TIME - TEMP
    MIN_COMPUT = MIN(TEMP,MIN_COMPUT)
  
    !store all the respective times
    COMM_STORE(curr_store) = COMM_TIME
    COMPUT_STORE(curr_store) = COMPUT_TIME
    SL_STORE(curr_store) = SL_TIME
    IDLE_STORE(curr_store) = CURR_TIME - START_TIME -COMM_TIME - COMPUT_TIME -SL_TIME
  
    !reset timers
    START_TIME = CURR_TIME
    SL_TIME = 0.D0
    COMM_TIME = 0.D0
    COMPUT_TIME = 0.D0
  
    CURR_STORE = CURR_STORE + 1

    RETURN
  END SUBROUTINE CLOCK_RESET

  SUBROUTINE END_TIME()
    IMPLICIT NONE
    ! finish timings
    COMPUT_TIME = COMPUT_TIME - CURR_TIME
    STOP_TIME = DCLOCK()
    COMPUT_TIME = COMPUT_TIME + STOP_TIME
    IDLE_TIME = STOP_TIME - START_TIME -COMM_TIME -COMPUT_TIME
    RETURN
  END SUBROUTINE END_TIME

  SUBROUTINE WRITE_TIME( MYPROC )
    IMPLICIT NONE
    INTEGER, INTENT(IN) :: MYPROC
    INTEGER :: I, F_UNIT

    CHARACTER(LEN=10) :: t_file

    !open file
    t_file = 'timer00000'
    write(t_file(6:10),"(i4.4)") myproc
    F_UNIT = MYPROC + 10000
    OPEN(unit=F_UNIT,file=t_file)

    

    WRITE(F_UNIT, *) MYPROC,TIME_START, MIN_COMPUT
    WRITE(F_UNIT, *) MYPROC, MIN_COMM
    
    DO I = 1,NSTORES
       WRITE(F_UNIT, *) MYPROC, I, 1, COMPUT_STORE(I)
       WRITE(F_UNIT, *) MYPROC, I, 2, COMM_STORE(I)
       WRITE(F_UNIT, *) MYPROC, I, 3, IDLE_STORE(I)
       WRITE(F_UNIT, *) MYPROC, I, 4, SL_STORE(I)
    ENDDO

    CLOSE(F_UNIT)

    RETURN
  END SUBROUTINE WRITE_TIME
END MODULE TIMER
